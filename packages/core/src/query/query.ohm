QueryLang {

  Query = OrQuery OrderByClause? LimitClause?

  sourceCharacter = any

  lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"

  identifier = ~reservedWord identifierName
  identifierName = identifierStart identifierPart*

  identifierStart = letter | "$" | "_" 
  identifierPart = identifierStart | unicodeCombiningMark
                 | unicodeDigit | unicodeConnectorPunctuation
                 | "\u200C" | "\u200D"
  letter += unicodeCategoryNl
  unicodeCategoryNl
    = "\u2160".."\u2182" | "\u3007" | "\u3021".."\u3029"
  unicodeDigit (a digit)
    = "\u0030".."\u0039" | "\u0660".."\u0669" | "\u06F0".."\u06F9" | "\u0966".."\u096F" | "\u09E6".."\u09EF" | "\u0A66".."\u0A6F" | "\u0AE6".."\u0AEF" | "\u0B66".."\u0B6F" | "\u0BE7".."\u0BEF" | "\u0C66".."\u0C6F" | "\u0CE6".."\u0CEF" | "\u0D66".."\u0D6F" | "\u0E50".."\u0E59" | "\u0ED0".."\u0ED9" | "\u0F20".."\u0F29" | "\uFF10".."\uFF19"

  unicodeCombiningMark (a Unicode combining mark)
    = "\u0300".."\u0345" | "\u0360".."\u0361" | "\u0483".."\u0486" | "\u0591".."\u05A1" | "\u05A3".."\u05B9" | "\u05BB".."\u05BD" | "\u05BF".."\u05BF" | "\u05C1".."\u05C2" | "\u05C4".."\u05C4" | "\u064B".."\u0652" | "\u0670".."\u0670" | "\u06D6".."\u06DC" | "\u06DF".."\u06E4" | "\u06E7".."\u06E8" | "\u06EA".."\u06ED" | "\u0901".."\u0902" | "\u093C".."\u093C" | "\u0941".."\u0948" | "\u094D".."\u094D" | "\u0951".."\u0954" | "\u0962".."\u0963" | "\u0981".."\u0981" | "\u09BC".."\u09BC" | "\u09C1".."\u09C4" | "\u09CD".."\u09CD" | "\u09E2".."\u09E3" | "\u0A02".."\u0A02" | "\u0A3C".."\u0A3C" | "\u0A41".."\u0A42" | "\u0A47".."\u0A48" | "\u0A4B".."\u0A4D" | "\u0A70".."\u0A71" | "\u0A81".."\u0A82" | "\u0ABC".."\u0ABC" | "\u0AC1".."\u0AC5" | "\u0AC7".."\u0AC8" | "\u0ACD".."\u0ACD" | "\u0B01".."\u0B01" | "\u0B3C".."\u0B3C" | "\u0B3F".."\u0B3F" | "\u0B41".."\u0B43" | "\u0B4D".."\u0B4D" | "\u0B56".."\u0B56" | "\u0B82".."\u0B82" | "\u0BC0".."\u0BC0" | "\u0BCD".."\u0BCD" | "\u0C3E".."\u0C40" | "\u0C46".."\u0C48" | "\u0C4A".."\u0C4D" | "\u0C55".."\u0C56" | "\u0CBF".."\u0CBF" | "\u0CC6".."\u0CC6" | "\u0CCC".."\u0CCD" | "\u0D41".."\u0D43" | "\u0D4D".."\u0D4D" | "\u0E31".."\u0E31" | "\u0E34".."\u0E3A" | "\u0E47".."\u0E4E" | "\u0EB1".."\u0EB1" | "\u0EB4".."\u0EB9" | "\u0EBB".."\u0EBC" | "\u0EC8".."\u0ECD" | "\u0F18".."\u0F19" | "\u0F35".."\u0F35" | "\u0F37".."\u0F37" | "\u0F39".."\u0F39" | "\u0F71".."\u0F7E" | "\u0F80".."\u0F84" | "\u0F86".."\u0F87" | "\u0F90".."\u0F95" | "\u0F97".."\u0F97" | "\u0F99".."\u0FAD" | "\u0FB1".."\u0FB7" | "\u0FB9".."\u0FB9" | "\u20D0".."\u20DC" | "\u20E1".."\u20E1" | "\u302A".."\u302F" | "\u3099".."\u309A" | "\uFB1E".."\uFB1E" | "\uFE20".."\uFE23"

  unicodeConnectorPunctuation = "\u005F" | "\u203F".."\u2040" | "\u30FB" | "\uFE33".."\uFE34" | "\uFE4D".."\uFE4F" | "\uFF3F" | "\uFF65"
  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"

  reservedWord = keyword
  keyword = kwMy | kwOpp | kwAll | kwCharacter | kwSummon
    | kwCombat | kwSupport | kwStatus | kwEquipment | kwAny
    | kwActive | kwPrev | kwNext | kwStandby | kwIncludes 
    | kwDefeated | kwWeapon | kwElement | kwNation | kwWith
    | kwTag | kwId | kwDefinition | kwAt | kwHas
    | kwRecentFrom | kwOrderBy | kwLimit | kwNot | kwAnd
    | kwOr | kwOf

  stringLiteral = "\"" doubleStringCharacter* "\""
  doubleStringCharacter = ~("\"" | "\\" | lineTerminator) sourceCharacter -- nonEscape
    | "\\" escapeSequence --escape
    
  escapeSequence = "\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v"

  numericLiteral = decimalIntegerLiteral "." decimalDigit* --bothParts
    | "." decimalDigit+     -- decimalsOnly
    | decimalIntegerLiteral -- integerOnly
  decimalIntegerLiteral = nonZeroDigit decimalDigit* -- nonZero
    | "0"  -- zero
  decimalDigit = "0".."9"
  nonZeroDigit = "1".."9"

  kwMy = "my" ~identifierPart
  kwOpp = "opp" ~identifierPart
  kwAll = "all" ~identifierPart
  kwCharacter = "character" "s"? ~identifierPart
  kwSummon = "summon" "s"? ~identifierPart
  kwCombat = "combat" "s"? ~identifierPart
  kwSupport = "support" "s"? ~identifierPart
  kwStatus = "status" "es"? ~identifierPart
  kwEquipment = "equipment" "s"? ~identifierPart
  kwAny = "any" ~identifierPart
  kwActive = "active" ~identifierPart
  kwPrev = "prev" ~identifierPart
  kwNext = "next" ~identifierPart
  kwStandby = "standby" ~identifierPart
  kwIncludes = "include" "s"? ~identifierPart
  kwDefeated = "defeated" ~identifierPart
  kwWeapon = "weapon" ~identifierPart
  kwElement = "element" ~identifierPart
  kwNation = "nation" ~identifierPart
  kwWith = "with" ~identifierPart
  kwTag = "tag" "s"? ~identifierPart
  kwId = "id" ~identifierPart
  kwDefinition = "definition" ~identifierPart
  kwAt = "at" ~identifierPart
  kwHas = "has" ~identifierPart
  kwRecentFrom = "recent" space "opp" space "from" ~identifierPart
  kwOrderBy = "order" space "by" ~identifierPart
  kwLimit = "limit" ~identifierPart
  kwNot = "not" ~identifierPart
  kwAnd = "and" ~identifierPart
  kwOr = "or" ~identifierPart
  kwOf = "of" ~identifierPart

  PropertyName = identifier | stringLiteral 
  ExternalPropertyName = identifier | decimalIntegerLiteral

  PrimaryExpression = PropertyName
    | numericLiteral
    | "(" Expression ")" -- paren
  UnaryExpression = "-" UnaryExpression -- neg
    | PrimaryExpression
  MultiplicativeExpression = MultiplicativeExpression "*" UnaryExpression -- mul
    | MultiplicativeExpression "/" UnaryExpression -- div
    | UnaryExpression
  AdditiveExpression = AdditiveExpression "+" MultiplicativeExpression -- add
    | AdditiveExpression "-" MultiplicativeExpression -- sub
    | MultiplicativeExpression
  Expression = AdditiveExpression

  PrimaryQuery = WhoSpecifier? TypeSpecifier WithSpecifier? -- canonical
    | WhoSpecifier? WithSpecifier -- canonicalAny
    | "@" NonemptyListOf<ExternalPropertyName, "."> -- external
    | "(" Query ")" -- paren
  WhoSpecifier = kwAll? kwMy -- my
    | kwAll? kwOpp -- opp
    | kwAll -- all
  TypeSpecifier = CharacterSpecifier | EntitySpecifier
  CharacterSpecifier = kwCharacter (kwIncludes kwDefeated)? -- chIncDeath
    | PositionSpecifier kwCharacter? -- position
    | kwDefeated kwCharacter? -- onlyDeath
  PositionSpecifier = kwActive | kwPrev | kwNext | kwStandby
  EntitySpecifier = kwSummon -- summon
    | kwCombat kwStatus -- combatStatus
    | kwStatus -- status
    | kwSupport -- support
    | kwEquipment -- equipment
    | kwAny -- any

  WithSpecifier = kwWith WithBody

  WithBody = kwId "="? Expression    -- id
    | kwDefinition kwId "="? Expression -- defId
    | kwTag TagSpecifier -- tag
    | PropertyName relativeOperator Expression -- prop
    
  relativeOperator = ">=" | "<=" | "!=" | "=" | ">" | "<"

  TagSpecifier = "(" NonemptyListOf<identifierName, ","> ")" -- direct
    | TagQualifier? kwOf "(" Query ")" -- indirect
    
  TagQualifier = kwWeapon | kwElement | kwNation

  UnaryQuery = kwNot UnaryQuery -- not
    | kwRecentFrom UnaryQuery -- recentFrom
    | kwHas UnaryQuery -- has
    | kwAt UnaryQuery -- at
    | PrimaryQuery
  RelationalQuery = RelationalQuery kwHas UnaryQuery -- has
    | RelationalQuery kwAt UnaryQuery -- at
    | UnaryQuery
  AndQuery = AndQuery kwAnd RelationalQuery -- and
    | RelationalQuery
  OrQuery = OrQuery kwOr AndQuery -- or
    | AndQuery


  OrderByClause = kwOrderBy NonemptyListOf<Expression, ",">
  LimitClause = kwLimit decimalIntegerLiteral
}
